_default:
    #!/bin/sh
    # Move to invocation_directory(), to ensure that invocation_directory() is preserved
    # on nested just call.
    cd {{invocation_directory()}}
    just --choose -f {{justfile()}} --chooser "fzf --preview 'just -f {{justfile()}} --show {} | bat -l make -p --color=always'"

# Return to single-pane layout.
layout_reset:
    tmux kill-pane -a

# Main debugger/editor pane with two side panes.
layout_dbg:
    #!/bin/sh
    # Move to invocation_directory() so new panes are opened at the correct path:
    cd {{invocation_directory()}}
    tmux kill-pane -a        \; \
         split-window -h     \; \
         resize-pane -R 20   \; \
         split-window -v     \; \
         select-pane -t 1

# Editor pane with one bottom console pane.
layout_dev:
    #!/bin/sh
    # Move to invocation_directory() so new panes are opened at the correct path:
    cd {{invocation_directory()}}
    tmux kill-pane -a        \; \
         split-window -v     \; \
         resize-pane -y 15   \; \
         select-pane -t 1

# Reload tmux config.
reload:
    -tmux source ~/.config/tmux/tmux.conf
    @echo "Press any key to continue.."
    @read

# Kill tmux server.
kill_server:
    tmux kill-server

# Setup TMUX package manager.
tpm_setup:
    -git clone https://github.com/tmux-plugins/tpm ~/.config/tmux/plugins/tpm
    @echo "Press any key to continue.."
    @read

# Install TPM plugins.
tpm_install_plugins:
    -~/.config/tmux/plugins/tpm/bin/install_plugins
    @echo "Press any key to continue.."
    @read

# Update TPM plugins.
tpm_update_plugins:
    -~/.config/tmux/plugins/tpm/bin/update_plugins all
    @echo "Press any key to continue.."
    @read
