_default:
    just --choose -f {{justfile()}} --chooser "fzf --preview 'just -f {{justfile()}} --show {} | bat -l make -p --color=always'" 

# Return to single-pane layout.
layout_reset:
    tmux kill-pane -a        \;

# Main debugger/editor pane with two side panes.
layout_dbg:
    tmux kill-pane -a        \; \
         split-window -h     \; \
         resize-pane -R 20   \; \
         split-window -v     \; \
         select-pane -t 1

# Editor pane with one bottom console pane.
layout_dev:
    tmux kill-pane -a        \; \
         split-window -v     \; \
         resize-pane -y 6    \; \
         select-pane -t 1

# Create a new main session.
new_main:
    #!/bin/sh
    if ! tmux has-session -t=main 2> /dev/null; then
        tmux new-session -ds main -c $HOME
    fi
    tmux switch-client -t main
